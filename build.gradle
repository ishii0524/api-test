apply plugin: "groovy"
apply plugin: "java"

version = "1.0"
description = "Test"

// Spock works with Java 1.5 and above
sourceCompatibility = 1.5

repositories {
  // Spock releases are available from Maven Central
  mavenCentral()
  // Spock snapshots are available from the Sonatype OSS snapshot repository
  maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
  // for Spock Reports Extension
  jcenter()
}

dependencies {
  // mandatory dependencies for using Spock
  compile "org.codehaus.groovy:groovy-all:2.4.1"
  testCompile "org.spockframework:spock-core:1.0-groovy-2.4"

  // optional dependencies for using Spock
  testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
  testRuntime "cglib:cglib-nodep:3.1"          // allows mocking of classes (in addition to interfaces)
  testRuntime "org.objenesis:objenesis:2.1"    // allows mocking of classes without default constructor (together with CGLIB)

  // dependencies used by examples in this project
  //testRuntime "com.h2database:h2:1.4.182"
  compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'

  testCompile( 'com.athaydes:spock-reports:1.2.8' ) {
    transitive = false // this avoids affecting your version of Groovy/Spock
  }
  testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.13'
}

ext {
  environments = ["localhost", "dev", "stag", "ci", "prod"]
}

environments.each { env ->
  task "${env}"(type: Test) {
    description "${env}環境への試験実行する."

    doFirst{
      systemProperty "scheme", getProperty("${env}.scheme")
      systemProperty "host", getProperty("${env}.host")
      systemProperty "port", getProperty("${env}.port")
    }
  }
}

defaultTasks 'localhost'
